plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id "com.vanniktech.maven.publish" version "0.31.0"
}

java {
    withJavadocJar()
    withSourcesJar()
}


group = 'de.codebarista'
version = '0.0.1'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'
    testImplementation 'org.xmlunit:xmlunit-core:2.10.0'
    testImplementation 'org.xmlunit:xmlunit-assertj:2.10.0'

}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.DEFAULT)
    signAllPublications()
    coordinates("de.codebarista.gallop", "gallop", "0.0.1-SNAPSHOT")

    pom {
        name = 'Gallop'
        description = 'Java library for creating electronic invoices compliant to the XRechnung standard'
        url = 'https://github.com/codebarista-de/gallop'
        inceptionYear = '2024'
        licenses {
            license {
                name = 'MIT License'
                url = 'https://opensource.org/licenses/MIT'
            }
        }
        developers {
            developer {
                id = 'codebarista-de'
                name = 'Codebarista'
                email = 'info@codebarista.de'
            }
        }
        scm {
            connection = 'scm:git:https://github.com/codebarista-de/gallop.git'
            developerConnection = 'scm:git:ssh://github.com/codebarista-de/gallop.git'
            url = 'https://github.com/codebarista-de/gallop'
        }
    }
}

tasks.register('printVersion') {
    doLast {
        println project.version
    }
}
