plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'org.jreleaser' version '1.17.0'
}

java {
    withJavadocJar()
    withSourcesJar()
}


group = 'de.codebarista'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'
    testImplementation 'org.xmlunit:xmlunit-core:2.10.0'
    testImplementation 'org.xmlunit:xmlunit-assertj:2.10.0'

}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

publishing {
    repositories {
//        maven { // TODO: Github Packages
//            name = "GitHubPackages"
//            url = uri("https://maven.pkg.github.com/codebarista-de/gallop")
//            credentials {
//                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
//                password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
//            }
//        }
        maven {
            name = "MavenCentral"
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
    publications {
//        mavenJava(MavenPublication) { // TODO: Github Packages
//            from components.java
//        }
        maven(MavenPublication) {
            groupId = 'de.codebarista'
            artifactId = 'gallop'

            from components.java

            pom {
                name = 'Gallop'
                description = 'Java library for creating electronic invoices compliant to the XRechnung standard'
                url = 'https://github.com/codebarista-de/gallop'
                inceptionYear = '2024'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'codebarista'
                        name = 'Codebarista'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/codebarista-de/gallop.git'
                    developerConnection = 'scm:git:ssh://github.com/codebarista-de/gallop.git'
                    url = 'https://github.com/codebarista-de/gallop'
                }
            }
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
        passphrase = System.getenv("JRELEASER_GPG_PASSPHRASE")
        publicKey = System.getenv("JRELEASER_GPG_PUBLIC_KEY")
        secretKey = System.getenv("JRELEASER_GPG_SECRET_KEY")
    }
    deploy {
        maven {
            // Portal Publisher API
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('target/staging-deploy')
                }
            }
        }
    }
}

tasks.register('printVersion') {
    doLast {
        println project.version
    }
}
